prefix drugbank: <http://bio2rdf.org/drugbank_vocabulary:>
prefix graphdl:  <http://ugritlab.ugr.es/graphdl#>
prefix dcterms:  <http://purl.org/dc/terms/>

construct {
  ?i  a graphdl:Edge ;
         graphdl:source ?d1 ;
         graphdl:target ?d2 .
  ?d1 a graphdl:Node ;
         graphdl:hasAttributeValue
                [graphdl:forAttribute graphdl:id;
                 graphdl:val ?d1_name ] .
  ?d2 a graphdl:Node ;
         graphdl:hasAttributeValue
                [graphdl:forAttribute graphdl:id;
                 graphdl:val ?d2_name ] .
} where {
  ?d1 drugbank:ddi-interactor-in ?i ;
    dcterms:title ?d1_name  ;
    a drugbank:Drug .
  ?d2 drugbank:ddi-interactor-in ?i ;
    dcterms:title ?d2_name ;
    a drugbank:Drug .

  filter(str(?d1) != str(?d2)) .
}